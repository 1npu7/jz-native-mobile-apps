apply plugin: 'com.android.application'
apply plugin: 'jacoco'

repositories {
    mavenCentral()
    flatDir {
        dirs '../third_party/AndroidSlidingUpPanel/libs/'
    }
    flatDir {
        dirs '../third_party/svg-android/libs/'
    }
    flatDir {
        dirs '../third_party/sticky-headers/libs/'
    }
    flatDir {
        dirs '../third_party/material-components-android/libs/'
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "no.schedule.javazone.v3"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true

        buildConfigField("boolean", "ENABLE_DEBUG_TOOLS", "false")

        buildConfigField("String", "WIFI_SSID", "\"${wifi_ssid}\"")
        buildConfigField("String", "WIFI_PASSPHRASE", "\"${wifi_passphrase}\"")
        buildConfigField("String", "CONFERENCE_HASHTAG", "\"${conference_hashtag}\"")
        buildConfigField("String", "CONFERENCE_HASHTAG_PREFIX", "\"${conference_hashtag_prefix}\"")
        buildConfigField("String", "BOOTSTRAP_DATA_TIMESTAMP", "\"${bootstrap_data_timestamp}\"")
        buildConfigField("String", "DEFAULT_TIMEZONE", "\"${default_timezone}\"")
        buildConfigField("String", "CONFERENCE_DAY1_START", "\"${conference_day1_start}\"")
        buildConfigField("String", "CONFERENCE_DAY1_END", "\"${conference_day1_end}\"")
        buildConfigField("String", "CONFERENCE_DAY2_START", "\"${conference_day2_start}\"")
        buildConfigField("String", "CONFERENCE_DAY2_END", "\"${conference_day2_end}\"")

        resValue("string", "pref_conf_messages_enabled", "pref_conf_messages_enabled_${conf_version}")
        buildConfigField("String", "PREF_CONF_MESSAGES_ENABLED", "\"pref_conf_messages_enabled_${conf_version}\"")

        resValue("string", "pref_attendee_at_venue", "pref_attendee_at_venue_int_${conf_version}")
        buildConfigField("String", "PREF_ATTENDEE_AT_VENUE", "\"pref_attendee_at_venue_int_${conf_version}\"")

        resValue("string", "pref_last_registration_check_ts", "pref_last_registration_check_ts_${conf_version}")
        buildConfigField("String", "PREF_LAST_REGISTRATION_CHECK_TS", "\"pref_last_registration_check_ts_${conf_version}\"")

        resValue("string", "pref_session_reminders_enabled", "pref_session_reminders_enabled_${conf_version}")
        buildConfigField("String", "PREF_NOTIFICATIONS_ENABLED", "\"pref_session_reminders_enabled_${conf_version}\"")

        resValue("string", "pref_session_feedback_reminders_enabled", "pref_session_feedback_reminders_enabled_${conf_version}")
        buildConfigField("String", "PREF_SESSION_FEEDBACK_REMINDERS_ENABLED", "\"pref_session_feedback_reminders_enabled_${conf_version}\"")

        buildConfigField("String", "PREF_SESSIONS_LOADED_DONE", "\"pref_sessions_loaded_done_${conf_version}\"")
        buildConfigField("String", "KEYNOTE_SESSION_ID", "\"${keynote_session_id}\"")
        buildConfigField("String", "SLEEPING_PILL_URL", "\"${sleepingpill_api_url}\"")


        buildConfigField("String", "NEW_FEED_ITEM", "\"new_feed_item\"")

        consumerProguardFiles file('proguard-project.txt')


    }
    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            buildConfigField("boolean", "ENABLE_DEBUG_TOOLS", "true")
            buildConfigField("String", "SLEEPING_PILL_SLUG_URL", "\"${developer_api_endpoint}\"")
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField("String", "SLEEPING_PILL_SLUG_URL", "\"${production_api_endpoint}\"")
        }
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        // Exclude files to avoid packaging collisions (same file name in multiple dependencies).
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }

    // Test Android jar comes with unimplemented methods that throw exceptions by default. This
    // option forces the methods to return default values instead. Required for static methods,
    // such as TextUtils, as those cannot be mocked with Mockito.
    // Refer http://tools.android.com/tech-docs/unit-testing-support#TOC-Method-...-not-mocked.-
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:support-vector-drawable:25.3.1'
    compile "com.android.support:cardview-v7:25.3.1"
    compile "com.android.support:support-v13:25.3.1"
    compile "com.android.support:recyclerview-v7:25.3.1"
    compile "com.android.support:preference-v7:25.3.1"
    compile "com.android.support:transition:25.3.1"
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'



    // JSON utility library.
    compile 'com.google.code.gson:gson:2.8.0'

    // Glide library, used to handle asynchronous loading and caching of images.
    compile 'com.github.bumptech.glide:glide:3.7.0'


    compile('com.google.api-client:google-api-client:1.22.0') {
        // Dependency org.apache.httpcomponents:httpclient:4.0.1
        // conflicts with the internal version provided by Android.
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }


    // Google Play services split client libs.
    compile "com.google.android.gms:play-services-analytics:11.0.2"
    compile "com.google.android.gms:play-services-auth:11.0.2"
    compile "com.google.android.gms:play-services-maps:11.0.2"
    compile "com.google.android.gms:play-services-plus:11.0.2"


    compile files('../third_party/disklrucache/libs/disklrucache-2.0.2.jar')
    compile files('../third_party/basic-http-client/libs/basic-http-client-android-0.88.jar')
    // SVG processor for the Conference Map overlay.
    compile(name: 'svg-android-2.0.7-20150515.214425-1', ext: 'jar')

    compile(name: 'StickyHeaders-release', ext: 'aar')

    // Google Maps Android API utility library
    compile('com.google.maps.android:android-maps-utils:0.4+') {
        exclude group: "com.google.android.gms"
    }

    // Added to resolve conflicts between the app build and the test build. The app build resolves
    // the jsr305 dependency to version 1.3.9, but in the test build the espresso-core dependency
    // resolves jsr305 to 2.0.1. Declaring a compile dependency on jsr305 2.0.1 will ensure 2.0.1 or
    // greater is used for both app and test builds.
    compile 'com.google.code.findbugs:jsr305:2.0.1'

    compile 'com.google.android:flexbox:0.2.6'

    compile ('uk.co.chrisjenx:calligraphy:2.2.0') {
        exclude group: 'com.android.support:design'
    }
    compile 'com.airbnb.android:lottie:2.0.0-beta4'

    // Firebase libraries
    compile "com.google.firebase:firebase-core:11.0.2"
    compile "com.google.firebase:firebase-messaging:11.0.2"
    compile "com.google.firebase:firebase-appindexing:11.0.2"
    compile "com.google.firebase:firebase-database:11.0.2"
    compile "com.google.firebase:firebase-config:11.0.2"
    compile "com.google.firebase:firebase-auth:11.0.2"
    compile "com.google.firebase:firebase-config:11.0.2"
    compile "com.google.firebase:firebase-crash:11.0.2"


    compile "org.apache.commons:commons-lang3:3.5"


    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-core:1.3'
    testCompile 'org.mockito:mockito-core:1.10.8'
    testCompile 'org.powermock:powermock-api-mockito:1.6.4'
    testCompile 'org.powermock:powermock-module-junit4-rule-agent:1.6.4'
    testCompile 'org.powermock:powermock-module-junit4-rule:1.6.4'
    testCompile 'org.powermock:powermock-module-junit4:1.6.4'
    testCompile 'org.json:json:20090211'
}
